#include <cuda_runtime.h>

#include "system/system.h"
#include "system/state.h"
#include "msld/msld.h"
#include "run/run.h"
#include "system/potential.h"
#include "domdec/domdec.h"
#include "main/defines.h"
#include "main/real3.h"



#ifdef DOUBLE
#define fasterfc erfc
#else
// Directly from CHARMM source code, charmm/source/domdec_gpu/gpu_utils.h
#warning "From CHARMM, not fully compatible"
static __forceinline__ __device__ float __internal_fmad(float a, float b, float c)
{
#if __CUDA_ARCH__ >= 200
  return __fmaf_rn (a, b, c);
#else // __CUDA_ARCH__ >= 200
  return a * b + c;
#endif // __CUDA_ARCH__ >= 200
}

// Following inline functions are copied from PMEMD CUDA implementation.
// Credit goes to:
/*             Scott Le Grand (NVIDIA)             */
/*               Duncan Poole (NVIDIA)             */
/*                Ross Walker (SDSC)               */
//
// Faster ERFC approximation courtesy of Norbert Juffa. NVIDIA Corporation
static __forceinline__ __device__ float fasterfc(float a)
{
  /* approximate log(erfc(a)) with rel. error < 7e-9 */
  float t, x = a;
  t =                       (float)-1.6488499458192755E-006;
  t = __internal_fmad(t, x, (float)2.9524665006554534E-005);
  t = __internal_fmad(t, x, (float)-2.3341951153749626E-004);
  t = __internal_fmad(t, x, (float)1.0424943374047289E-003);
  t = __internal_fmad(t, x, (float)-2.5501426008983853E-003);
  t = __internal_fmad(t, x, (float)3.1979939710877236E-004);
  t = __internal_fmad(t, x, (float)2.7605379075746249E-002);
  t = __internal_fmad(t, x, (float)-1.4827402067461906E-001);
  t = __internal_fmad(t, x, (float)-9.1844764013203406E-001);
  t = __internal_fmad(t, x, (float)-1.6279070384382459E+000);
  t = t * x;
  return exp2f(t);
}
#endif

#define WARPSPERBLOCK 1
__host__ __device__ inline
bool check_proximity(DomdecBlockVolume a,real3 b,real c2)
{
  real bufferA,bufferB,buffer2;

  bufferB=b.x-a.max.x; // Distance one way
  bufferA=a.min.x-b.x; // Distance the other way
  bufferA=(bufferA>bufferB?bufferA:bufferB);
  bufferA=(bufferA<0?0:bufferA);
  buffer2=bufferA*bufferA;

  bufferB=b.y-a.max.y; // Distance one way
  bufferA=a.min.y-b.y; // Distance the other way
  bufferA=(bufferA>bufferB?bufferA:bufferB);
  bufferA=(bufferA<0?0:bufferA);
  buffer2+=bufferA*bufferA;

  bufferB=b.z-a.max.z; // Distance one way
  bufferA=a.min.z-b.z; // Distance the other way
  bufferA=(bufferA>bufferB?bufferA:bufferB);
  bufferA=(bufferA<0?0:bufferA);
  buffer2+=bufferA*bufferA;

  return buffer2<=c2;
}

template <bool useSoftCore>
// __global__ void getforce_nbdirect_kernel(int startBlock,int endBlock,int maxPartnersPerBlock,int *blockBounds,int *blockPartnerCount,struct DomdecBlockPartners *blockPartners,struct DomdecBlockVolume *blockVolume,struct NbondPotential *nbonds,int vdwParameterCount,struct VdwPotential *vdwParameters,int *blockExcls,struct Cutoffs cutoffs,real3 *position,real3 *force,real3 box,real *lambda,real *lambdaForce,real *energy)
__global__ void getforce_nbdirect_kernel(
  int startBlock,
  int endBlock,
  int maxPartnersPerBlock,
  const int* __restrict__ blockBounds,
  const int* __restrict__ blockPartnerCount,
  const struct DomdecBlockPartners* __restrict__ blockPartners,
  const struct DomdecBlockVolume* __restrict__ blockVolume,
  const struct NbondPotential* __restrict__ nbonds,
  int vdwParameterCount,
#ifdef USE_TEXTURE
  cudaTextureObject_t vdwParameters,
#else
  const struct VdwPotential* __restrict__ vdwParameters,
#endif
  const int* __restrict__ blockExcls,
  struct Cutoffs cutoffs,
  const real3* __restrict__ position,
  real3_f* __restrict__ force,
  real3 box,
  const real* __restrict__ lambda,
  real_f* __restrict__ lambdaForce,
  real_e* __restrict__ energy)
{
// NYI - maybe energy should be a double
  int i=blockIdx.x*blockDim.x+threadIdx.x;
  int iWarp=i>>5; // i/32
  int iThread=i-32*iWarp;
  int iBlock=(iWarp>>WARPSPERBLOCK)+startBlock;
  int jBlock;
  int iCount,jCount,jtmpCount;
  int jUsed, jtmpBlockThread;
  __shared__ struct DomdecBlockVolume iBlockVolume;
  __shared__ struct DomdecBlockVolume jBlockVolume[1<<WARPSPERBLOCK];
  __shared__ int jnext;
  int ii,jj;
  int j,jmax;
  int tmp;
  real r,rinv;
  char4 shift;
  real3 dr;
  NbondPotential inp,jnp;
  real itmpnp_q,jtmpnp_q;
  int itmpnp_typeIdx,jtmpnp_typeIdx;
  real fij,eij;
  real lEnergy=0;
  // extern __shared__ real sEnergy[];
  real3 xi,xitmp,xj,xjtmp;
  real3 fi,fitmp,fjtmp;
  real fli,flitmp,fljtmp;
  int bi,bitmp,bj,bjtmp;
  real li,litmp,lj,ljtmp,lixljtmp;
  real rEff,dredr,dredll; // Soft core stuff
  int exclAddress, exclMask, tmpExclMask;

  if (iBlock<endBlock && threadIdx.x==0) iBlockVolume=blockVolume[iBlock];
  if (iBlock<endBlock && threadIdx.x==0) jnext=0;
  __syncthreads();
  if (iBlock<endBlock) {
    ii=blockBounds[iBlock];
    iCount=blockBounds[iBlock+1]-ii;
    // ii+=(iThread);
    if ((iThread)<iCount) {
      // inp=nbonds[ii];
      // xi=position[ii];
      inp=nbonds[32*iBlock+iThread];
      xi=position[32*iBlock+iThread];
      bi=inp.siteBlock;
      li=1;
      if (bi) li=lambda[0xFFFF & bi];
    }
    // iBlockVolume=blockVolume[iBlock];

    fi=real3_reset<real3>();
    fli=0;

    // used i/32 instead of iBlock to shift to beginning of array
    jmax=blockPartnerCount[iWarp>>WARPSPERBLOCK];
    jCount=32;
    jUsed=32;
    // j=recitfy_modulus(iWarp,1<<WARPSPERBLOCK);
    if (iThread==0) j=atomicInc((unsigned int*)(&jnext),0xFFFFFFFF);
    j=__shfl_sync(0xFFFFFFFF,j,0);
    // for (j=rectify_modulus(iWarp,1<<WARPSPERBLOCK); (j<jmax)||(jUsed<jCount); j+=(1<<WARPSPERBLOCK))
    for (; (j<jmax)||(jUsed<32);) {
      // if (iThread==0 && iWarp < 2) printf("Line %d, iWarp=%d, j=%d, jmax=%d, jCount=%d, jUsed=%d, jtmpCount=%d\n",__LINE__,iWarp,j,jmax,jCount,jUsed,jtmpCount); // DEBUG
      jtmpCount=0;
      bool iFlag=false;
      while ((jtmpCount<32)&&((j<jmax)||(jUsed<32))) {
      // ----- Load new j data if necessary ----- //
      // if (iThread==0 && iWarp < 2) printf("Line %d, iWarp=%d, j=%d, jmax=%d, jCount=%d, jUsed=%d, jtmpCount=%d\n",__LINE__,iWarp,j,jmax,jCount,jUsed,jtmpCount); // DEBUG
      if (jUsed==32) {
      // if (iThread==0 && iWarp < 2) printf("Line %d, iWarp=%d, j=%d, jmax=%d, jCount=%d, jUsed=%d, jtmpCount=%d\n",__LINE__,iWarp,j,jmax,jCount,jUsed,jtmpCount); // DEBUG
      jBlock=blockPartners[maxPartnersPerBlock*(iWarp>>WARPSPERBLOCK)+j].jBlock;
      shift=blockPartners[maxPartnersPerBlock*(iWarp>>WARPSPERBLOCK)+j].shift;
      // boxShift.x*=box.x;
      // boxShift.y*=box.y;
      // boxShift.z*=box.z;
      exclAddress=blockPartners[maxPartnersPerBlock*(iWarp>>WARPSPERBLOCK)+j].exclAddress;
      if (exclAddress==-1) {
        exclMask=0xFFFFFFFF;
      } else {
        exclMask=blockExcls[32*exclAddress+(iThread)];
      }
      if (iBlock==jBlock && shift.x==0 && shift.y==0 && shift.z==0) {
        exclMask>>=(iThread+1);
        exclMask<<=(iThread+1);
      }
      jj=blockBounds[jBlock];
      jCount=blockBounds[jBlock+1]-jj;
      // jj+=(iThread);
      // jBlockThread=32*jBlock+iThread;
      jUsed=0;
      if ((iThread)<jCount) {
        // jnp=nbonds[jj];
        // xj=position[jj];
        jnp=nbonds[32*jBlock+iThread];
        xj=position[32*jBlock+iThread];
        // // real3_inc(&xj,boxShift);
        // xj.x+=boxShift.x;
        // xj.y+=boxShift.y;
        // xj.z+=boxShift.z;
        xj.x+=shift.x*box.x;
        xj.y+=shift.y*box.y;
        xj.z+=shift.z*box.z;
        bj=jnp.siteBlock;
        lj=1;
        if (bj) lj=lambda[0xFFFF & bj];
      }
      if (iThread==0) {
        jBlockVolume[threadIdx.x>>5]=blockVolume[jBlock];
        jBlockVolume[threadIdx.x>>5].min.x+=shift.x*box.x;
        jBlockVolume[threadIdx.x>>5].min.y+=shift.y*box.y;
        jBlockVolume[threadIdx.x>>5].min.z+=shift.z*box.z;
        jBlockVolume[threadIdx.x>>5].max.x+=shift.x*box.x;
        jBlockVolume[threadIdx.x>>5].max.y+=shift.y*box.y;
        jBlockVolume[threadIdx.x>>5].max.z+=shift.z*box.z;
      }
      __syncwarp();
      }
      // ----- Squeeze j - only load relevant j into jtmp ----- //
      iFlag|=check_proximity(jBlockVolume[threadIdx.x>>5],xi,cutoffs.rCut*cutoffs.rCut);
      // iFlag=true; // DEBUG
      bool jFlag=check_proximity(iBlockVolume,xj,cutoffs.rCut*cutoffs.rCut);
      // jFlag=true; // DEBUG
      jFlag&=(iThread<jCount);
      jFlag&=(iThread>=jUsed);
      // cumsum method 3
      int jFlags=__ballot_sync(0xFFFFFFFF,jFlag);
      // if (iThread==0 && (iWarp==100 || iWarp==101)) printf("iWarp=%d, jFlags=%08X, jCount=%d, jUsed=%d, j=%d, jmax=%d\n",iWarp,jFlags,jCount,jUsed,j,jmax);
      int cumjFlag=__popc(((1<<iThread)-1)&jFlags)+jFlag;
      int available=__popc(jFlags);
      // if (iThread==31 && cumjFlag!=available) printf("Error in cumjFlag\n"); // DEBUG
      // cumsum method 4
      // int cumjFlag=jFlag;
      // cumjFlag+=(iThread>=1)*__shfl_up_sync(0xFFFFFFFF,cumjFlag,1);
      // cumjFlag+=(iThread>=2)*__shfl_up_sync(0xFFFFFFFF,cumjFlag,2);
      // cumjFlag+=(iThread>=4)*__shfl_up_sync(0xFFFFFFFF,cumjFlag,4);
      // cumjFlag+=(iThread>=8)*__shfl_up_sync(0xFFFFFFFF,cumjFlag,8);
      // cumjFlag+=(iThread>=16)*__shfl_up_sync(0xFFFFFFFF,cumjFlag,16);
      // int available=__shfl_sync(0xFFFFFFFF,cumjFlag,31);
      // End cumsum
      int target=iThread-jtmpCount;
      if (target>=available) target=-1;
      int targetThread=0;
      targetThread+=16*(target>=__shfl_sync(0xFFFFFFFF,cumjFlag-jFlag,targetThread+16));
      targetThread+=8*(target>=__shfl_sync(0xFFFFFFFF,cumjFlag-jFlag,targetThread+8));
      targetThread+=4*(target>=__shfl_sync(0xFFFFFFFF,cumjFlag-jFlag,targetThread+4));
      targetThread+=2*(target>=__shfl_sync(0xFFFFFFFF,cumjFlag-jFlag,targetThread+2));
      targetThread+=(target>=__shfl_sync(0xFFFFFFFF,cumjFlag-jFlag,targetThread+1));
      // if (iThread==0 && iWarp < 2) printf("Line %d, iWarp=%d.00, jFlags=0x%08X, cumjFlag=%d, target=%d, targetThread=%d\n",__LINE__,iWarp,jFlags,cumjFlag,target,targetThread); // DEBUG
      // if (iThread==8 && iWarp < 2) printf("Line %d, iWarp=%d.08, jFlags=0x%08X, cumjFlag=%d, target=%d, targetThread=%d\n",__LINE__,iWarp,jFlags,cumjFlag,target,targetThread); // DEBUG
      // if (iThread==16 && iWarp < 2) printf("Line %d, iWarp=%d.16, jFlags=0x%08X, cumjFlag=%d, target=%d, targetThread=%d\n",__LINE__,iWarp,jFlags,cumjFlag,target,targetThread); // DEBUG
      // if (iThread==24 && iWarp < 2) printf("Line %d, iWarp=%d.24, jFlags=0x%08X, cumjFlag=%d, target=%d, targetThread=%d\n",__LINE__,iWarp,jFlags,cumjFlag,target,targetThread); // DEBUG
      // int debugit1=__shfl_sync(0xFFFFFFFF,jFlag,targetThread); // DEBUG
      // if (target>=0 && !debugit1) printf("Error, fetching a target with jFlag false\n"); // DEBUG
      // int debugit2=__shfl_sync(0xFFFFFFFF,cumjFlag,targetThread); // DEBUG
      // if (target>=0 && debugit2-1 != target) printf("Error, cumsum-self=%d is not equal to target=%d\n",debugit2-debugit1,target); // DEBUG
      // ----- Actually fetch the data ----- //
      real passnp_q=__shfl_sync(0xFFFFFFFF,jnp.q,targetThread);
      if (target>=0) jtmpnp_q=passnp_q;
      int passnp_typeIdx=__shfl_sync(0xFFFFFFFF,jnp.typeIdx,targetThread);
      if (target>=0) jtmpnp_typeIdx=passnp_typeIdx;
      real3 xpass;
      xpass.x=__shfl_sync(0xFFFFFFFF,xj.x,targetThread);
      xpass.y=__shfl_sync(0xFFFFFFFF,xj.y,targetThread);
      xpass.z=__shfl_sync(0xFFFFFFFF,xj.z,targetThread);
      if (target>=0) xjtmp=xpass;
      int bpass=__shfl_sync(0xFFFFFFFF,bj,targetThread);
      if (target>=0) bjtmp=bpass;
      real lpass=__shfl_sync(0xFFFFFFFF,lj,targetThread);
      if (target>=0) ljtmp=lpass;
      int passExclMask=__shfl_sync(0xFFFFFFFF,exclMask,targetThread);
      if (target>=0) tmpExclMask=passExclMask;
      if (target>=0) jtmpBlockThread=32*jBlock+targetThread;
      // ----- And update information on bounds... ----- //
      jUsed=__popc(__ballot_sync(0xFFFFFFFF,cumjFlag<=(32-jtmpCount))); // 32-jtmpCount is available space
      // if (jUsed != __popc(__ballot_sync(0xFFFFFFFF,target>=0))) printf("Whoops\n"); // DEBUG
      // if (jUsed != __popc(__ballot_sync(0xFFFFFFFF,target>=0))) printf("Whoops: Line %d, iWarp=%d, iThread=%d, jFlags=0x%08X, cumjFlag=%d, target=%d, targetThread=%d, jUsed=%d, jUsed(alt)=%d\n",__LINE__,iWarp,iThread,jFlags,cumjFlag,target,targetThread,jUsed,__popc(__ballot_sync(0xFFFFFFFF,target>=0))); // DEBUG
      // if (iWarp==100 || iWarp==101) printf("Whoops: Line %d, iWarp=%d, iThread=%d, jFlags=0x%08X, cumjFlag=%d, target=%d, targetThread=%d, jUsed=%d, jUsed(alt)=%d, jtmpCount=%d, fetchedthing=%08X\n",__LINE__,iWarp,iThread,jFlags,cumjFlag,target,targetThread,jUsed,__popc(__ballot_sync(0xFFFFFFFF,target>=0)),jtmpCount,__ballot_sync(0xFFFFFFFF,cumjFlag<=(32-jtmpCount))); // DEBUG
      jtmpCount+=available;
      if (jtmpCount>=32) jtmpCount=32;
      if (jtmpCount<32) {
        // We need more data, go on to next block
        if (iThread==0) j=atomicInc((unsigned int*)(&jnext),0xFFFFFFFF);
        j=__shfl_sync(0xFFFFFFFF,j,0);
      }
      // if (iThread==0 && iWarp < 2) printf("Line %d, iWarp=%d, j=%d, jmax=%d, jCount=%d, jUsed=%d, jtmpCount=%d\n",__LINE__,iWarp,j,jmax,jCount,jUsed,jtmpCount); // DEBUG
      }
        
      fjtmp=real3_reset<real3>();
      fljtmp=0;

      for (tmp=0; tmp<iCount; tmp++) {
        if (__shfl_sync(0xFFFFFFFF,iFlag,tmp)) {
          itmpnp_q=__shfl_sync(0xFFFFFFFF,inp.q,tmp);
          itmpnp_typeIdx=__shfl_sync(0xFFFFFFFF,inp.typeIdx,tmp);
          xitmp.x=__shfl_sync(0xFFFFFFFF,xi.x,tmp);
          xitmp.y=__shfl_sync(0xFFFFFFFF,xi.y,tmp);
          xitmp.z=__shfl_sync(0xFFFFFFFF,xi.z,tmp);
          bitmp=__shfl_sync(0xFFFFFFFF,bi,tmp);
          litmp=__shfl_sync(0xFFFFFFFF,li,tmp);

          fitmp=real3_reset<real3>();
          flitmp=0;
          if (iThread<jtmpCount && ((1<<tmp)&tmpExclMask)) {
#ifdef USE_TEXTURE
            struct VdwPotential vdwp;
            ((real2*)(&vdwp))[0]=tex1Dfetch<real2>(vdwParameters,jtmpnp_typeIdx*vdwParameterCount+itmpnp_typeIdx);
#else
            struct VdwPotential vdwp=vdwParameters[jtmpnp_typeIdx*vdwParameterCount+itmpnp_typeIdx];
#endif

            // Geometry
            dr=real3_sub(xitmp,xjtmp);
            r=real3_mag<real>(dr);

            if (r<cutoffs.rCut) {
              // Scaling
              if ((bitmp&0xFFFF0000)==(bjtmp&0xFFFF0000)) {
                if (bitmp==bjtmp) {
                  lixljtmp=litmp;
                } else {
                  lixljtmp=0;
                }
              } else {
                lixljtmp=litmp*ljtmp;
              }

              rEff=r;
              if (useSoftCore) {
                dredr=1; // d(rEff) / d(r)
                dredll=0; // d(rEff) / d(lixljtmp)
                if (bitmp || bjtmp) {
                  // real rSoft=(2.0*ANGSTROM*sqrt(4.0))*(1.0-lixljtmp);
                  real rSoft=SOFTCORERADIUS*(1-lixljtmp);
                  if (r<rSoft) {
                    real rdivrs=r/rSoft;
                    rEff=1-((real)0.5)*rdivrs;
                    rEff=rEff*rdivrs*rdivrs*rdivrs+((real)0.5);
                    dredr=3-2*rdivrs;
                    dredr*=rdivrs*rdivrs;
                    dredll=rEff-dredr*rdivrs;
                    // dredll*=-(2.0*ANGSTROM*sqrt(4.0));
                    dredll*=-SOFTCORERADIUS;
                    rEff*=rSoft;
                  }
                }
              }
              rinv=1/rEff;

              // interaction
                // Electrostatics
              /*fij=-kELECTRIC*inp.q*jtmpnp_q*rinv*rinv;
              if (bi || bjtmp || energy) {
                eij=kELECTRIC*inp.q*jtmpnp_q*rinv;
              }*/
              real br=cutoffs.betaEwald*rEff;
              // real erfcrinv=erfcf(br)*rinv;
              real erfcrinv=fasterfc(br)*rinv;
              // fij=-kELECTRIC*inp.q*jtmpnp_q*(erfcrinv+(2/sqrt(M_PI))*cutoffs.betaEwald*expf(-br*br))*rinv;
              // fij=-kELECTRIC*inp.q*jtmpnp_q*(erfcrinv+1.128379167095513f*cutoffs.betaEwald*expf(-br*br))*rinv;
              // fij=-kELECTRIC*inp.q*jtmpnp_q*(erfcrinv+((real)(2/sqrt(M_PI)))*cutoffs.betaEwald*expf(-br*br))*rinv;
              fij=-kELECTRIC*itmpnp_q*jtmpnp_q*(erfcrinv+((real)1.128379167095513)*cutoffs.betaEwald*expf(-br*br))*rinv;
              if (bitmp || bjtmp || energy) {
                eij=kELECTRIC*itmpnp_q*jtmpnp_q*erfcrinv;
              }
                // Van der Waals
              real rinv3=rinv*rinv*rinv;
              real rinv6=rinv3*rinv3;
              /*fij+=-(12*(vdwp.c12*rinv6)-6*(vdwp.c6))*rinv6*rinv;
              if (bi || bjtmp || energy) {
                eij+=(vdwp.c12*rinv6-vdwp.c6)*rinv6;
              }*/
              // See charmm/source/domdec/enbxfast.F90, functions calc_vdw_constants, vdw_attraction, vdw_repulsion
              real rCut3=cutoffs.rCut*cutoffs.rCut*cutoffs.rCut;
              real rSwitch3=cutoffs.rSwitch*cutoffs.rSwitch*cutoffs.rSwitch;
              if (rEff<cutoffs.rSwitch) {
                fij+=(6*vdwp.c6-12*vdwp.c12*rinv6)*rinv6*rinv;
                if (bitmp || bjtmp || energy) {
                  real dv6=1/(rCut3*rSwitch3);
                  eij+=vdwp.c12*(rinv6*rinv6-dv6*dv6)-vdwp.c6*(rinv6-dv6);
                }
              } else {
                real k6=rCut3/(rCut3-rSwitch3);
                real k12=rCut3*rCut3/(rCut3*rCut3-rSwitch3*rSwitch3);
                real rCutinv3=1/rCut3;
                fij+=(6*vdwp.c6*k6*(rinv3-rCutinv3)*rinv3-12*vdwp.c12*k12*(rinv6-rCutinv3*rCutinv3)*rinv6)*rinv;
                if (bitmp || bjtmp || energy) {
                  eij+=vdwp.c12*k12*(rinv6-rCutinv3*rCutinv3)*(rinv6-rCutinv3*rCutinv3)-vdwp.c6*k6*(rinv3-rCutinv3)*(rinv3-rCutinv3);
                }
              }
              fij*=lixljtmp;

              // Lambda force
              if (bitmp || bjtmp) {
                if (useSoftCore) {
                  flitmp=eij+fij*dredll;
                } else {
                  flitmp=eij;
                }
                if ((bitmp&0xFFFF0000)==(bjtmp&0xFFFF0000)) {
                  if (bitmp==bjtmp) {
                    fljtmp+=flitmp;
                  }
                  flitmp=0;
                } else {
                  fljtmp+=litmp*flitmp;
                  flitmp*=ljtmp;
                }
              }

              // Spatial force
              if (useSoftCore) {
                rinv=1/r;
                fij*=dredr;
              }
              real3_scaleinc(&fjtmp, -fij*rinv,dr);
              fitmp=real3_scale<real3>(fij*rinv,dr);

              // Energy, if requested
              if (energy) {
                // if (!(lixljtmp*eij>-5000000)) printf("lixljtmp*eij=%f lixljtmp=%f eij=%f\n",lixljtmp*eij,lixljtmp,eij);
                lEnergy+=lixljtmp*eij;
              }
            }
          }
          __syncwarp();
          fitmp.x+=__shfl_xor_sync(0xFFFFFFFF,fitmp.x,1);
          fitmp.x+=__shfl_xor_sync(0xFFFFFFFF,fitmp.x,2);
          fitmp.x+=__shfl_xor_sync(0xFFFFFFFF,fitmp.x,4);
          fitmp.x+=__shfl_xor_sync(0xFFFFFFFF,fitmp.x,8);
          fitmp.x+=__shfl_xor_sync(0xFFFFFFFF,fitmp.x,16);
          if (iThread==tmp) fi.x+=fitmp.x;
          fitmp.y+=__shfl_xor_sync(0xFFFFFFFF,fitmp.y,1);
          fitmp.y+=__shfl_xor_sync(0xFFFFFFFF,fitmp.y,2);
          fitmp.y+=__shfl_xor_sync(0xFFFFFFFF,fitmp.y,4);
          fitmp.y+=__shfl_xor_sync(0xFFFFFFFF,fitmp.y,8);
          fitmp.y+=__shfl_xor_sync(0xFFFFFFFF,fitmp.y,16);
          if (iThread==tmp) fi.y+=fitmp.y;
          fitmp.z+=__shfl_xor_sync(0xFFFFFFFF,fitmp.z,1);
          fitmp.z+=__shfl_xor_sync(0xFFFFFFFF,fitmp.z,2);
          fitmp.z+=__shfl_xor_sync(0xFFFFFFFF,fitmp.z,4);
          fitmp.z+=__shfl_xor_sync(0xFFFFFFFF,fitmp.z,8);
          fitmp.z+=__shfl_xor_sync(0xFFFFFFFF,fitmp.z,16);
          if (iThread==tmp) fi.z+=fitmp.z;
          if (bitmp) {
            flitmp+=__shfl_xor_sync(0xFFFFFFFF,flitmp,1);
            flitmp+=__shfl_xor_sync(0xFFFFFFFF,flitmp,2);
            flitmp+=__shfl_xor_sync(0xFFFFFFFF,flitmp,4);
            flitmp+=__shfl_xor_sync(0xFFFFFFFF,flitmp,8);
            flitmp+=__shfl_xor_sync(0xFFFFFFFF,flitmp,16);
            if (iThread==tmp) fli+=flitmp;
          }
        }
      }
      __syncwarp();
      if ((iThread)<jtmpCount) {
        if (bjtmp) {
          atomicAdd(&lambdaForce[0xFFFF & bjtmp],fljtmp);
        }
        // at_real3_inc(&force[32*jBlock+iThread],fjtmp);
        at_real3_inc(&force[jtmpBlockThread],fjtmp);
      }

      // if (iThread==0 && iWarp < 2) printf("Line %d, iWarp=%d, j=%d, jmax=%d, jCount=%d, jUsed=%d, jtmpCount=%d\n",__LINE__,iWarp,j,jmax,jCount,jUsed,jtmpCount); // DEBUG
      if (jUsed==32) {
        // We need more data, go on to next block
        if (iThread==0) j=atomicInc((unsigned int*)(&jnext),0xFFFFFFFF);
        j=__shfl_sync(0xFFFFFFFF,j,0);
      }
      // if (iThread==0 && iWarp < 2) printf("Line %d, iWarp=%d, j=%d, jmax=%d, jCount=%d, jUsed=%d, jtmpCount=%d\n",__LINE__,iWarp,j,jmax,jCount,jUsed,jtmpCount); // DEBUG
    }
    __syncwarp();
    if ((iThread)<iCount) {
      if (bi) {
        atomicAdd(&lambdaForce[0xFFFF & bi],fli);
      }
      at_real3_inc(&force[32*iBlock+iThread],fi);
    }
  }

  // Energy, if requested
  if (energy) {
    // Use of shared memory here causes error when getforce_nbrecip_gather is executed concurrently. Whatever CUDA...
    // #warning "Using reduction without shared memory"
    // real_sum_reduce(lEnergy,sEnergy,energy);
    real_sum_reduce(lEnergy,energy);
  }
}

void getforce_nbdirect(System *system,bool calcEnergy)
{
  system->domdec->pack_positions(system);
  system->domdec->recull_blocks(system);

  Potential *p=system->potential;
  State *s=system->state;
  Run *r=system->run;
  Domdec *d=system->domdec;
  int id=d->id;
  int startBlock=d->blockCount[id];
  int endBlock=d->blockCount[id+1];
  int N=endBlock-startBlock;
  int shMem=0;
  real_e *pEnergy=NULL;

  if (r->calcTermFlag[eenbdirect]==false) return;

  if (calcEnergy) {
    // shMem=(1<<WARPSPERBLOCK)*sizeof(real);
    shMem=0;
    pEnergy=s->energy_d+eenbdirect;
  }

  if (system->msld->useSoftCore) {
#ifdef USE_TEXTURE
    getforce_nbdirect_kernel<true><<<((32<<WARPSPERBLOCK)*N+(32<<WARPSPERBLOCK)-1)/(32<<WARPSPERBLOCK),(32<<WARPSPERBLOCK),shMem,r->nbdirectStream>>>(startBlock,endBlock,d->maxPartnersPerBlock,d->blockBounds_d,d->blockPartnerCount_d,d->blockPartners_d,d->blockVolume_d,d->localNbonds_d,p->vdwParameterCount,p->vdwParameters_tex,system->domdec->blockExcls_d,system->run->cutoffs,d->localPosition_d,d->localForce_d,s->orthBox_f,s->lambda_fd,s->lambdaForce_d,pEnergy);
#else
    getforce_nbdirect_kernel<true><<<((32<<WARPSPERBLOCK)*N+(32<<WARPSPERBLOCK)-1)/(32<<WARPSPERBLOCK),(32<<WARPSPERBLOCK),shMem,r->nbdirectStream>>>(startBlock,endBlock,d->maxPartnersPerBlock,d->blockBounds_d,d->blockPartnerCount_d,d->blockPartners_d,d->blockVolume_d,d->localNbonds_d,p->vdwParameterCount,p->vdwParameters_d,system->domdec->blockExcls_d,system->run->cutoffs,d->localPosition_d,d->localForce_d,s->orthBox_f,s->lambda_fd,s->lambdaForce_d,pEnergy);
#endif
  } else {
#ifdef USE_TEXTURE
    getforce_nbdirect_kernel<false><<<((32<<WARPSPERBLOCK)*N+(32<<WARPSPERBLOCK)-1)/(32<<WARPSPERBLOCK),(32<<WARPSPERBLOCK),shMem,r->nbdirectStream>>>(startBlock,endBlock,d->maxPartnersPerBlock,d->blockBounds_d,d->blockPartnerCount_d,d->blockPartners_d,d->blockVolume_d,d->localNbonds_d,p->vdwParameterCount,p->vdwParameters_tex,system->domdec->blockExcls_d,system->run->cutoffs,d->localPosition_d,d->localForce_d,s->orthBox_f,s->lambda_fd,s->lambdaForce_d,pEnergy);
#else
    getforce_nbdirect_kernel<false><<<((32<<WARPSPERBLOCK)*N+(32<<WARPSPERBLOCK)-1)/(32<<WARPSPERBLOCK),(32<<WARPSPERBLOCK),shMem,r->nbdirectStream>>>(startBlock,endBlock,d->maxPartnersPerBlock,d->blockBounds_d,d->blockPartnerCount_d,d->blockPartners_d,d->blockVolume_d,d->localNbonds_d,p->vdwParameterCount,p->vdwParameters_d,system->domdec->blockExcls_d,system->run->cutoffs,d->localPosition_d,d->localForce_d,s->orthBox_f,s->lambda_fd,s->lambdaForce_d,pEnergy);
#endif
  }

  system->domdec->unpack_forces(system);
}

template <typename real_type>
__global__ void getforce_nbdirect_reduce_kernel(int atomCount,int idCount,real_type *force)
{
  int i=blockIdx.x*blockDim.x+threadIdx.x;
  int j;
  real_type f=0;

  if (i<atomCount) {
    for (j=1; j<idCount; j++) {
      f+=force[j*atomCount+i];
    }
    atomicAdd(&force[i],f);
  }
}

void getforce_nbdirect_reduce(System *system,bool calcEnergy)
{
  State *s=system->state;
  Run *r=system->run;
  int N=3*s->atomCount+2*s->lambdaCount;

  getforce_nbdirect_reduce_kernel<<<(N+BLNB-1)/BLNB,BLNB,0,r->nbdirectStream>>>(N,system->idCount,s->forceBuffer_d);

  if (calcEnergy) {
    N=eeend;
    getforce_nbdirect_reduce_kernel<<<(N+BLNB-1)/BLNB,BLNB,0,r->nbdirectStream>>>(N,system->idCount,s->energy_d);
  }
}
